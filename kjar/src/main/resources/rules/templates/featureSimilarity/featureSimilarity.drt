template header
featureName
featureGetter

package templates.featureSimilarity

dialect "java"

import com.ftn.model.User;
import com.ftn.model.AudioFeatures;
import com.ftn.model.ScoreComponent;
import com.ftn.model.request.SeedTrackRequest;
import com.ftn.model.specification.ScoringSpecification;
import com.ftn.model.track.Track;
import com.ftn.model.track.TrackCandidate;

template "B5 - Seed feature similarity @{featureName}"

rule "b5_@{featureName}_score" salience 10
when
    $spec : ScoringSpecification( wSeedFeatures != null && wSeedFeatures > 0 )

    // candidate track we're scoring
    $cand : TrackCandidate( $tid : trackId )
    $track : Track( id == $tid, features != null )

    // seed track context
    $seedReq : SeedTrackRequest( seedTrackId != null )
    $seed : Track( id == $seedReq.seedTrackId, features != null )

    // numeric bindings (Java)
    $trackVal : Double() from ( $track.getFeatures().@{featureGetter}() )
    $seedVal  : Double() from ( $seed.getFeatures().@{featureGetter}() )
    $sim      : Double() from ( 1.0 - Math.abs($trackVal - $seedVal) )
then
    insert( new ScoreComponent(
        $cand.getUserId(),
        $tid,
        ScoreComponent.Source.SEED_FEATURES,
        $sim * $spec.getWSeedFeatures()
    ) );
end

end template