package rules.cep

import java.util.UUID

declare GenreLiked
    @role( event )
    @timestamp( timestamp )
    @expires( 8h )
    userId    : UUID
    genre     : UUID
    timestamp : java.time.Instant
end

declare GenreDisliked
    @role( event )
    @timestamp( timestamp )
    @expires( 8h )
    userId    : UUID
    genre     : UUID
    timestamp : java.time.Instant
end

rule "G1: positive genre affinity in last hour -> GenreLiked"
when
    $seed : GenreAffinity( $u : userId, $g : genre )
    Number( intValue > 10 ) from accumulate(
        GenreAffinity( userId == $u, genre == $g, this after[0s, 1h] $seed ),
        sum( score )
    )
    not GenreLiked( userId == $u, genre == $g, this after[0s, 1h] $seed )
then
    insert( new GenreLiked( $u, $g, java.time.Instant.now() ) );
end

rule "G2: negative genre affinity in last hour -> GenreDisliked"
when
    $seed : GenreAffinity( $u : userId, $g : genre )
    Number( intValue < -5 ) from accumulate(
        GenreAffinity( userId == $u, genre == $g, this after[0s, 1h] $seed ),
        sum( score )
    )
    not GenreDisliked( userId == $u, genre == $g, this after[0s, 1h] $seed )
then
    insert( new GenreDisliked( $u, $g, java.time.Instant.now() ) );
end
